# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Programming

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

alias nginx_stop='sudo nginx -s stop'
alias nginx_start='sudo nginx'
alias nginx_restart='nginx_stop && nginx_start'
alias fpm_stop='sudo killall -c php-fpm'
alias fpm_start='sudo php-fpm'
alias fpm_restart='fpm_stop && fpm_start'
alias web_stop='nginx_stop && fpm_stop'
alias web_start='nginx_start && fpm_start'
alias web_restart='nginx_restart && fpm_restart'
