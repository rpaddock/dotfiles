export DOTFILES=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export PATH="./bin:/Users/rpaddock/.dotfiles/bin:/Users/rpaddock/.local/bin:/usr/local/opt/node@10/bin:/usr/local/opt/python/libexec/bin:/usr/local/opt/make/libexec/gnubin:/Library/Frameworks/Mono.framework/Versions/Current/Commands:/usr/local/opt/mongodb-community@3.6/bin:/usr/local/opt/mysql@5.7/bin/$PATH"
# export MANPATH="/usr/local/man:$MANPATH"

# Start new tmux session
function tn() {
  if [ -z "$1" ]; then;
    session_name=$(basename `pwd`)
  else
    session_name=$1
  fi
  tmux new-session -s $session_name -n 'main'
}

unsetopt correct_all
unsetopt correct


. /usr/local/etc/profile.d/z.sh

bindkey -e

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh